module 'Touch (microbit)' Input
author MicroBlocks
version 1 1 
description 'Capacitive touch sensing for micro:bit v1 and v2.'
variables _lowCount _isHigh 

  spec 'r' 'touch' 'pin _ touched : threshold _' 'num num' 0 0
  spec 'r' 'logo touched' 'logo touched'

to 'logo touched' {
  if ((boardType) == 'micro:bit') {return (booleanConstant false)}
  return (touch 26 0)
}

to touch pin optionalThreshold {
  comment 'Discharge pin'
  digitalWriteOp pin false
  local 'threshold' 0
  if ((pushArgCount) > 1) {
    threshold = optionalThreshold
  }
  if (threshold <= 0) {
    comment 'Select threshold based on board type'
    if ((boardType) == 'micro:bit') {
      threshold = 9
    } else {
      threshold = 15
    }
  }
  local 'n' 0
  comment 'Wait for pin to become high, incrementing n'
  repeatUntil (digitalReadOp pin) {
    n += 1
    if (n >= threshold) {
      comment 'Long charging time means pin is touched'
      _isHigh = (booleanConstant true)
      _lowCount = 0
      digitalWriteOp pin false
      return _isHigh
    }
  }
  digitalWriteOp pin false
  if (_isHigh == 0) {
    _isHigh = (booleanConstant false)
  }
  comment 'count is < threshold so pin not touched'
  if _isHigh {
    comment 'Require several low counts in a row
to exit "isHigh" state to avoid jitter.'
    _lowCount += 1
    if (_lowCount > 2) {
      _isHigh = (booleanConstant false)
    }
  }
  return _isHigh
}

