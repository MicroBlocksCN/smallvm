module 'Smart Home'
author 'Josep Ferràndiz'
version 1 1
depends 'LCD Display' 'Temperature Humidity (DHT11, DHT22)' Tone Servo NeoPixel 'RFID (RC522)'

choices smarthome_fan clockwise 'counter-clockwise' stop
choices smarthome_notes c 'c#' d 'd#' e f 'f#' g 'g#' a 'a#' b
choices smarthome_power 'voltage (V)' 'current (mA)' 'power (mW)'
choices smarthome_lr left right
choices smarthome_window open close
choices smarthome_onoff on off

description 'Library for Keyestudio IoT Smart Home Kit for ESP32

Connexion values----------------------------------
Fan: -IN io18, +IN io19    Led: io12
Steam sensor: io34         NeoPixel (4 leds): io26
PIR: 14                            Gas sensor: io23
Left button: io16              Right button: io27
DHT11: io17                    Buzzer: io25
Servo (window): io5        Servo (door): io13
RFID: I2C                        LCD: I2C
--------------------------------------------------

https://docs.keyestudio.com/projects/KS5009/en/latest/docs/index.html#
'
variables _smarthomeInitialized

  spec ' ' 'smarthome_setFan' 'Home fan _' 'menu.smarthome_fan' 'clockwise'
  spec ' ' 'smarthome_displayText' 'Home display _ in line _' 'str num' 'ESP32 Smart Home' 1
  spec ' ' 'smarthome_setWindow' 'Home _ the window' 'menu.smarthome_window' 'open'
  spec ' ' 'smarthome_setDoor' 'Home activate the door'
  spec ' ' 'smarthome_playNote' 'Home play _ octave _ for _ ms' 'menu.smarthome_notes num num' 'c' 0 100
  spec ' ' 'smarthome_setLight' 'Home set light _' 'menu.smarthome_onoff' 'on'
  space
  spec 'r' 'smarthome_temperature' 'Home temperature ºC'
  spec 'r' 'smarthome_humidity' 'Home humidity'
  spec 'r' 'smarthome_steamLevel' 'Home steam level'
  space
  spec 'r' 'smarthome_readRFID' 'Home read RFID card : _' 'bool' true
  spec 'r' 'smarthome_motion' 'Home movement detected?'
  spec 'r' 'smarthome_button' 'Home _ button pressed?' 'menu.smarthome_lr' 'left'
  spec 'r' 'smarthome_gasDetected' 'Home gas detected?'
  space
  spec ' ' 'smarthome_setNeoPixels' 'Home set NeoPixels _ _ _ _' 'color color color color'
  spec ' ' 'smarthome_clearNeoPixels' 'Home clear NeoPixels'
  space
  spec 'r' 'smarthome_electricConsumption' 'Home electric consumption _' 'menu.smarthome_power' 'voltage (V)'

to '_initialize' {
  if (not _smarthomeInitialized) {
    local _pinBuzzer 25
    'attach buzzer to pin' _pinBuzzer
    local _pinNeopixel 26
    neoPixelAttach 4 _pinNeopixel
    local _addrRFID 40
    rc522_initialize_I2C _addrRFID
    _smarthomeInitialized = (booleanConstant true)
  }
}

to 'smarthome_readRFID' beep {
  _initialize
  if (rc522_card_present) {
    if beep {
      'play tone' 'c' 0 60
    }
    return (rc522_read_uid)
  }
}

to 'smarthome_button' button {
  if (button == 'left') {
    local 'pin' 16
  } (button == 'right') {
    local 'pin' 27
  } else {
    return false
  }
  return (not (digitalReadOp pin))
}

to smarthome_clearNeoPixels {
  _initialize
  clearNeoPixels
}

to smarthome_displayText text line {
  LCD1602_write text line
  waitMillis 1000
  for i ((size text) - 15) {
    LCD1602_write ('[data:copyFromTo]' text i (16 + i)) line
    waitMillis 350
  }
}

to smarthome_setDoor {
  local _pinServoDoor 13
  setServoAngle _pinServoDoor -90
  waitMillis 1000
  setServoAngle _pinServoDoor 90
  waitMillis 1000
  stopServo pin
}

to 'smarthome_electricConsumption' magnitude {
  local _pinVo 36
  local _pinV 39
  local 'Vo' ('read samples' 500 _pinVo)
  local 'V' ('read samples' 500 _pinV)
  local 'Vout' ((V * 323) / 500)
  local 'Vsen' (Vo - V)
  local 'Iout' (Vsen * 17)
  local 'value' 0
  if (magnitude == 'voltage (V)') {
    value = ('[data:join]' (Vout / 100) ',' (Vout % 100))
  }
  if (magnitude == 'current (mA)') {
    value = Iout
  }
  if (magnitude == 'power (mW)') {
    value = (((Vout * 10) * Iout) / 1000)
  }
  return value
}

to smarthome_setFan state {
  local _pinFanPos 19
  local _pinFanNeg 18
  if (state == 'clockwise') {
    digitalWriteOp _pinFanNeg true
    digitalWriteOp _pinFanPos false
  }
  if (state == 'counter-clockwise') {
    digitalWriteOp _pinFanNeg false
    digitalWriteOp _pinFanPos true
  }
  if (state == 'stop') {
    digitalWriteOp _pinFanNeg false
    digitalWriteOp _pinFanPos false
  }
}

to 'smarthome_gasDetected' {
  local _pinGas 23
  return (not (digitalReadOp _pinGas))
}

to smarthome_humidity {
  local _pinDHT11 17
  return (humidity_DHT11 _pinDHT11)
}

to smarthome_motion {
  local _pinPIR 14
  return (digitalReadOp _pinPIR)
}

to 'smarthome_playNote' note octave time {
  _initialize
  'play tone' note octave time
}

to 'read samples' sampleCount pin {
  local 'sum' 0
  repeat sampleCount {
    sum += (analogReadOp pin)
  }
  return (sum / sampleCount)
}

to 'smarthome_setLight' state {
  local _pinLED 12
  if (state == 'on') {digitalWriteOp _pinLED true}
  if (state == 'off') {digitalWriteOp _pinLED false}
}

to 'smarthome_setNeoPixels' c1 c2 c3 c4 {
  _initialize
  setNeoPixelColors10 c1 c2 c3 c4 (colorSwatch 35 190 30 255) (colorSwatch 35 190 30 255) (colorSwatch 35 190 30 255) (colorSwatch 35 190 30 255) (colorSwatch 35 190 30 255) (colorSwatch 35 190 30 255)
}

to 'smarthome_steamLevel' {
  local _pinSteam 34
  return (analogReadOp _pinSteam)
}

to smarthome_temperature {
  local _pinDHT11 17
  return (temperature_DHT11 _pinDHT11)
}

to smarthome_setWindow position {
  local _pinServoWindow 5
  if (position == 'open') {setServoAngle _pinServoWindow -90}
  if (position == 'close') {setServoAngle _pinServoWindow 90}
  waitMillis 2000
}
