module 'Simple BLE Radio' Comm
author wwj718
version 1 0 
depends 'BLE Scanner' 
description 'Simple BLE Radio
'
variables _octoIsBeaming _BLERadio_group _SBR_message_history 

  spec ' ' 'octoSendBeam' 'beam _ : duration _ ms' 'str num' 'text' 100
  spec 'r' 'octoReceiveBeam' 'receive message'
  space
  spec 'r' '_octoMakeMessage' '_octoMakeMessage _' 'str' 'text'
  spec 'r' '_octoReceiveScan' '_last BLE advertisement'
  spec ' ' 'set group' 'set group _ (0-99)' 'num' 0
  spec 'r' '_isBLERadioMessage' '_isBLERadioMessage _' 'str' 'text'
  spec ' ' '_addIDToHistory' '_addIDToHistory _' 'str' 'text'
  spec 'r' '_IDNotYetSeen' '_IDNotYetSeen _' 'str' 'text'

to '_IDNotYetSeen' id {
  if (('[data:find]' id _SBR_message_history) == -1) {
    return (booleanConstant true)
  } else {
    return (booleanConstant false)
  }
}

to '_addIDToHistory' id {
  '[data:addLast]' id _SBR_message_history
  if ((size _SBR_message_history) > 100) {
    repeat 30 {
      '[data:delete]' 1 _SBR_message_history
    }
  }
}

to '_isBLERadioMessage' name {
  return (and (and (and (name != '') ((size name) == 16)) (name != '0000000000000000')) ('_IDNotYetSeen' name))
}

to '_octoMakeMessage' text {
  comment 'Mimic iOS, eg: ECA7059C00000001

8 位随机数
2 位 group
最后 6 位消息'
  local 'result' ('[data:makeList]')
  repeat 8 {
    '[data:addLast]' (at 'random' '0123456789ABCDEF') result
  }
  if (_BLERadio_group < 10) {
    '[data:addLast]' 0 result
  }
  '[data:addLast]' _BLERadio_group result
  if ((size text) <= 6) {
    '[data:addLast]' text result
    repeat (6 - (size text)) {
      '[data:addLast]' '%' result
    }
  }
  return ('[data:joinStrings]' result)
}

to '_octoReceiveScan' {
  comment 'Receive any BLE advertisement (not just Octo ones).
Return byte array with the last advertisment or false if none received'
  return ('[ble:scanReceive]')
}

to octoReceiveBeam {
  local 'group' 0
  local 'text' 0
  local 'name' ''
  if (_SBR_message_history == 0) {
    _SBR_message_history = ('[data:makeList]')
  }
  if (bleScan_scanReceived) {
    name = (bleScan_deviceName)
    if ('_isBLERadioMessage' (bleScan_deviceName)) {
      '_addIDToHistory' name
      local 'message' name
      group = ('[data:convertType]' ('[data:copyFromTo]' message 9 10) 'number')
      if (group == _BLERadio_group) {
        if (('[data:find]' '%' message) != -1) {
          return ('[data:copyFromTo]' message 11 (('[data:find]' '%' message) - 1))
        } else {
          return ('[data:copyFromTo]' message 11 16)
        }
      }
    }
  }
  return (booleanConstant false)
}

to octoSendBeam text optionalDuration {
  comment '最多支持6个字符串: 英文字母和数字'
  if ((size text) > 6) {
    sayIt '多于 6 个字符'
    return ''
  }
  local 'duration' 100
  if ((pushArgCount) > 1) {
    duration = optionalDuration
  }
  if (_octoIsBeaming == 1) {
    sayIt 'waiting...'
    waitUntil (_octoIsBeaming == 0)
    sayIt ''
  }
  _octoIsBeaming = 1
  callCustomCommand '[ble:octoStartBeam]' ('[data:makeList]' ('_octoMakeMessage' text))
  waitMillis duration
  callCustomCommand '[ble:octoStopBeam]'
  _octoIsBeaming = 0
}

to 'set group' n {
  if (n > 99) {
    _BLERadio_group = 99
  } else {
    _BLERadio_group = n
  }
}

